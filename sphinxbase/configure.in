dnl Welcome to the Sphinx automated build system.
dnl try not to hurt yourself ;)

AC_INIT(sphinxbase, 0.4)
AM_CONFIG_HEADER([include/config.h include/sphinx_config.h])
AM_INIT_AUTOMAKE([no-define dist-bzip2 dist-zip])

CFLAGS=${CFLAGS:--g -O2 -Wall}

AC_CANONICAL_HOST

dnl
dnl Set proper compiler flags and such for some platforms
dnl
case $host in
     arm-wince-pe*)
	CFLAGS=${CFLAGS:--march=armv4 -mapcs-32 -malignment-traps}
	LIBS='-lc -lgcc -lwinsock -lcoredll'
	CFLAGS="$CFLAGS -DNEWLIB -DSARM -DWIN32 -DGNUWINCE"
	;;
     *uclinux*)
	# uClinux needs special "flat" binaries
	LDFLAGS="$LDFLAGS -Wl,-elf2flt"
	;;
     *cygwin*|*mingw*)
        # Need to get the proper symbol defined for DLL (can't just
	# use DLL_EXPORT from libtool because other DLLs include our header
	# files...)
	CFLAGS="$CFLAGS -DSPHINXBASE_DLL"
        ;;
     *)
     ;;
esac     

AC_DEFUN([AC_C_THREAD_TLS],[
use_tls=false
AC_MSG_CHECKING([for __thread storage class])
AC_TRY_COMPILE([
__thread int thread_local_variable;
],[],[
  use_tls=true
  AC_MSG_RESULT(yes)
  AC_DEFINE(SPHINXBASE_TLS, [__thread], [Thread Local Storage attribute])
],[
  use_tls=true
  AC_DEFINE(SPHINXBASE_TLS, [], [Thread Local Storage attribute])
  AC_MSG_RESULT(no)
])
])

AC_ISC_POSIX
AC_PROG_CC
AM_PROG_CC_STDC
# Don't even bother until the rest of the world has an up-to-date Flex
#AM_PROG_LEX
AC_PROG_YACC
AC_HEADER_STDC
AC_WORDS_BIGENDIAN
AC_TYPE_SIGNAL
AC_CHECK_TYPES(long long)
AC_CHECK_SIZEOF(long long)
AC_CHECK_SIZEOF(long)
AC_CHECK_FUNCS([popen perror])
AC_CHECK_HEADER(errno.h)
AM_ICONV

dnl
dnl Check for Lapack stuff unless disabled
dnl
use_lapack=true
use_internal_lapack=false
AC_ARG_WITH(lapack,
	AC_HELP_STRING([--without-lapack],
		       [Disable matrix algebra support (depends on LAPACK)]),[
if test x$withval = xno; then
   use_lapack=false
fi
])
if test x$use_lapack = xtrue; then
   AC_CHECK_LIB(m, log)
   AC_CHECK_LIB(g2c, r_log)
   AC_CHECK_LIB(blas, sgemm_)
   AC_CHECK_LIB(lapack, sgesv_,,use_internal_lapack=true)
   AC_DEFINE(WITH_LAPACK, [], [Enable matrix algebra with LAPACK])
else
   AC_CHECK_LIB(m, log)
fi
AM_CONDITIONAL(LAPACK_LITE, test x$use_internal_lapack = xtrue)
AM_CONDITIONAL(USE_LAPACK, test x$use_lapack = xtrue)

dnl
dnl Enable thread-safe versions of some internal functions
dnl
threadsafe=auto
AC_ARG_ENABLE(threads,
	AC_HELP_STRING([--disable-threads],
		       [Disable thread-safe versions of memory allocation functions (default: check)]),[
if test x$enableval != x; then
   threadsafe=$enableval
fi
])
if test x$threadsafe != xno; then
   threads=false
   dnl
   dnl Can't seem to check for Windows API functions, but that's okay
   dnl because we know they will be there...
   dnl
   case $host in
     *cygwin*|*mingw*|*wince*)
	# GCC always uses __thread, even on Windows, so check for it below
	threads=true
     ;;
     *)
        AC_CHECK_HEADERS([pthread.h], [threads=true])
	AC_CHECK_LIB(pthread, pthread_create)
     ;;
   esac
   if test x$threads = xtrue; then
     AC_DEFINE(ENABLE_THREADS, [], [Enable thread safety])
     AC_C_THREAD_TLS()
     threadsafe=yes
   elif test x$threadsafe = xyes; then
     AC_MSG_ERROR(--enable-threads was given but test for thread functions failed!)
   else
     threadsafe=no
   fi
fi
AM_CONDITIONAL(USE_THREADS, test x$threadsafe = xyes)
AM_CONDITIONAL(USE_TLS, test x$use_tls = xtrue)

dnl
dnl Allow compilation for fixed or floating-point MFCC and GMM computation
dnl
fixed_point=false
AC_ARG_ENABLE(fixed,
	AC_HELP_STRING([--enable-fixed=RADIX],
		       [Use 32-bit fixed-point for MFCC and GMM computation,
		        optionally specifying a radix point]),[
if test x$enableval = xyes; then
   AC_DEFINE(FIXED_POINT)
   fixed_point=true
else
   AC_DEFINE(FIXED_POINT, [], [Use fixed-point computation])
   AC_DEFINE_UNQUOTED(DEFAULT_RADIX,$enableval,[Default radix point for fixed-point])
fi])
AM_CONDITIONAL(FIXED_POINT, test x$fixed_point = xtrue)

dnl
dnl Enable 16-bit fixed-point (Q15) format for MFCC (less accurate, more fast)
dnl
AC_ARG_ENABLE(fixed16,
	AC_HELP_STRING([--enable-fixed16],
		       [Use 16-bit fixed-point for MFCC computation]),[
if test x$enableval = xyes; then
   AC_DEFINE(FIXED_POINT, [], [Use fixed-point computation])
   AC_DEFINE(FIXED16, [], [Use Q15 fixed-point computation])
fi])

dnl
dnl determine audio type or use none if none supported on this platform
dnl

ad_files=""
ad_libs=""

dnl Added from suggestion by 
dnl Jasper van Veghel <jvveghel@vanboxtelsoftware.nl>, 02/03/2003:
AC_ARG_WITH(alsa,
           AC_HELP_STRING([--with-alsa], [Use ALSA library for sound I/O]),
           [ad_files="ad_alsa.lo"
            ad_backend="AD_BACKEND_ALSA"
            ad_libs="-lasound"
	    AC_DEFINE(AD_BACKEND_ALSA, [], [Use ALSA library for sound I/O])
            AC_CHECK_HEADER(alsa/asoundlib.h,,
                    AC_ERROR(ALSA header file <alsa/asoundlib.h> not found!))]
)

if test "x$ad_files" = "x"; then
        case $host in
                *-*-linux*|*-*-uclinux*)
                        ad_files="ad_oss.lo"
                        ad_backend="AD_BACKEND_OSS"
			AC_DEFINE(AD_BACKEND_OSS, [], [Use OSS interface for sound I/O])
                        ;;
                # FIXME: isn't this the same OSS as on Linux?
                *-*-freebsd*|*-*-netbsd*|*-*-openbsd*)
                        ad_files="ad_oss_bsd.lo"
                        ad_backend="AD_BACKEND_OSS_BSD"
			AC_DEFINE(AD_BACKEND_OSS_BSD, [], [Use OSS interface for sound I/O])
                        ;;
                *-*-osf*)
                        ad_files="ad_osf.lo"
                        ad_backend="AD_BACKEND_OSF"
			AC_DEFINE(AD_BACKEND_OSF, [], [Use OSF interface for sound I/O])
                        ;;
                *-*-irix*)
                        ad_files="ad_irix.lo"
                        ad_libs="-laudio"
                        ad_backend="AD_BACKEND_IRIX"
			AC_DEFINE(AD_BACKEND_IRIX, [], [Use IRIX interface for sound I/O])
                        ;;
                *-*-sunos4*)
                        ad_files="ad_sunos.lo audio_utils_sunos.lo"
                        ad_libs="-lm -lnsl -ldl"
                        ad_backend="AD_BACKEND_SUNOS"
			AC_DEFINE(AD_BACKEND_SUNOS, [], [Use SunOS interface for sound I/O])
                        ;;
                *-*-solaris*)
                        ad_files="ad_sunos.lo audio_utils_sunos.lo"
                        ad_libs="-lm -lnsl -lsocket -ldl"
                        ad_backend="AD_BACKEND_SUNOS"
			AC_DEFINE(AD_BACKEND_SUNOS, [], [Use SunOS interface for sound I/O])
                        ;;
                *-*-*cygwin*|*-*-*mingw*)
                        ad_files="play_win32.lo rec_win32.lo"
                        ad_libs="-lwinmm"
                        ad_backend="AD_BACKEND_WIN32"
			AC_DEFINE(AD_BACKEND_WIN32, [], [Use WinMM interface for sound I/O])
                        ;;
                *-*apple*-*)
                        ad_files="ad_portaudio.lo pa_convert.lo pa_lib.lo \
			          pa_mac_core.lo pa_trace.lo pablio.lo ringbuffer.lo"
                        ad_backend="AD_BACKEND_PORTAUDIO"
			ad_libs="-framework CoreAudio -framework AudioToolbox"
			AC_DEFINE(AD_BACKEND_PORTAUDIO, [], [Use PortAudio interface for sound I/O])
                        ;;
                *)
                        ad_files="ad_base.lo"
                        ad_backend="AD_BACKEND_NONE"
                        AC_MSG_RESULT([No audio interface for host type $host.])
			AC_DEFINE(AD_BACKEND_NONE, [], [No interface for sound I/O])
                        ;;
        esac
fi

AC_SUBST(ad_files)
AC_SUBST(ad_libs)
AC_SUBST(ad_backend)

AC_LIBTOOL_WIN32_DLL
AM_PROG_LIBTOOL

dnl
dnl Check for Doxygen, and build dox if present
dnl
AC_CHECK_PROG(HAVE_DOXYGEN, doxygen, yes, no)
AM_CONDITIONAL(BUILD_DOXYGEN, test "x$HAVE_DOXYGEN" = "xyes")

dnl
dnl Check for Python, and build python module if present
dnl
use_python=true
PYTHON=python
AC_ARG_WITH(python,
	AC_HELP_STRING([--without-python],
		       [Disable Python extension]),[
if test x$withval = xno; then
   use_python=false
   PYTHON=bogus
elif test x$withval = xyes; then
   # No specific python binary was given, so check for it in PATH
   use_python=true
   PYTHON=python
else
   # A python was given, assume it is in the user's PATH or is fully qualified
   use_python=true
   PYTHON="$withval"
fi
])
if test "x$use_python" = xtrue -a "x$PYTHON" = xpython; then
   AC_PATH_PROG(PYTHON, python, bogus)
fi
AM_CONDITIONAL(BUILD_PYTHON, test "$PYTHON" != "xbogus")
AC_SUBST(PYTHON)

dnl
dnl Check for Pyrex, and rebuild python module if present
dnl
if test x$HAVE_PYTHON = xyes; then
   AC_CHECK_PROG(HAVE_PYREX, pyrexc, yes, no)
fi
AM_CONDITIONAL(BUILD_PYREX, test "x$HAVE_PYREX" = "xyes")


AC_OUTPUT([
sphinxbase.pc
Makefile
include/Makefile
python/Makefile
python/setup.py
src/Makefile
src/libsphinxad/Makefile
src/libsphinxbase/Makefile
src/libsphinxbase/util/Makefile
src/libsphinxbase/feat/Makefile
src/libsphinxbase/fe/Makefile
src/libsphinxbase/lm/Makefile
src/sphinx_fe/Makefile
src/sphinx_cepview/Makefile
src/sphinx_jsgf2fsg/Makefile
src/sphinx_adtools/Makefile
src/sphinx_lmtools/Makefile
doc/Makefile
doc/doxyfile
test/Makefile
test/unit/testfuncs.sh
test/unit/Makefile
test/unit/test_alloc/Makefile
test/unit/test_bitvec/Makefile
test/unit/test_case/Makefile
test/unit/test_string/Makefile
test/unit/test_cmdln/Makefile
test/unit/test_hash/Makefile
test/unit/test_matrix/Makefile
test/unit/test_feat/Makefile
test/unit/test_fe/Makefile
test/unit/test_logmath/Makefile
test/unit/test_ngram/Makefile
test/unit/test_fsg/Makefile
test/unit/test_thread/Makefile
test/regression/testfuncs.sh
test/regression/Makefile
])
