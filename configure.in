dnl Welcome to the Sphinx-3 automated build system.
dnl try not to hurt yourself ;)

AC_INIT(configure.in)
AM_INIT_AUTOMAKE(sphinx3,0.6)

CFLAGS=${CFLAGS:--g -O2 -Wall -ansi}
#CFLAGS=${CFLAGS:--g -O2 -Wall  -DDMALLOC}

AC_ISC_POSIX
AC_PROG_CC
AM_PROG_CC_STDC
AC_C_BIGENDIAN
AC_HEADER_STDC

AC_C_CONST
AC_TYPE_SIZE_T
AC_CHECK_FUNCS(memmove bcopy)

AC_TYPE_SIGNAL


hmmdata=`ls -1 $srcdir/model/hmm/hub4_cd_continuous_8gau_1s_c_d_dd | egrep -v '(Makefile|CVS)'`
hmmdata=`echo $hmmdata`

tidigitsdata=`(cd $srcdir/model/hmm/tidigits && find . -type f -print | egrep -v '(Makefile|CVS|\.svn)')`
tidigitsdata=`echo $tidigitsdata`

lmdata=`ls -1 $srcdir/model/lm/an4 | egrep -v '(Makefile|CVS)' | sed 's/\.in//g'`
lmdata=`echo $lmdata`

docdata=`ls -1 $srcdir/doc | egrep -v '(Makefile|CVS)'`
docdata=`echo $docdata`

AC_CANONICAL_HOST

dnl
dnl Look for math libraries
dnl
AC_CHECK_LIB(m,log)
AC_CHECK_LIB(g2c,r_log)
AC_CHECK_LIB(blas,sgemm_)
AC_CHECK_LIB(lapack,sgesv_)

dnl
dnl determine audio type or use none if none supported on this platform
dnl

ad_files=""
ad_libs=""
audio_test_file="pittsburgh.littleendian"

dnl Added from suggestion by 
dnl Jasper van Veghel <jvveghel@vanboxtelsoftware.nl>, 02/03/2003:
AC_ARG_WITH(alsa,
           AC_HELP_STRING([--with-alsa], [Use ALSA library for sound I/O]),
           [ad_files="ad_alsa.lo"
            ad_backend="AD_BACKEND_ALSA"
            ad_libs="-lasound"
	    # We must remove -ansi because ALSA headers don't like it (is it really worth it?)
            CFLAGS="`echo $CFLAGS -DAD_BACKEND_ALSA | sed -e s,-ansi,,`"
            AC_CHECK_HEADER(sys/asoundlib.h,,
                    AC_ERROR(ALSA header file <sys/asoundlib.h> not found!))]
)
dnl This may also be useful
AC_ARG_WITH(esd,
           AC_HELP_STRING([--with-esd], [Use ESound daemon for sound I/O]),
           [ad_files="ad_esd.lo"
            ad_backend="AD_BACKEND_ESD"
            ad_libs="-lesd"
            CFLAGS="$CFLAGS -DAD_BACKEND_ESD"
            AC_CHECK_HEADER(esd.h,,
                    AC_ERROR(ESD header file <esd.h> not found!))]
)

dnl soundcard.h defaults to <soundcard.h>. If <sys/soundcard.h> is
dnl found instead, then use the latter.
AC_CHECK_HEADER(sys/soundcard.h,
	CFLAGS="$CFLAGS -DSYS_SOUNDCARD_H")

if test "x$ad_files" = "x"; then
        case $host in
                *-*-linux*)
                        ad_files="ad_oss.lo"
                        ad_backend="AD_BACKEND_OSS"
                        CFLAGS="$CFLAGS -DAD_BACKEND_OSS"
                        ;;
                # FIXME: isn't this the same OSS as on Linux?
                *-*-freebsd*|*-*-netbsd*|*-*-openbsd*)
                        ad_files="ad_oss_bsd.lo mulaw_base.lo"
                        ad_backend="AD_BACKEND_OSS_BSD"
                        CFLAGS="$CFLAGS -DAD_BACKEND_OSS_BSD"
dnl soundcard.h defaults to <soundcard.h>. If <sys/soundcard.h> is
dnl found instead, then use the latter.
			AC_CHECK_HEADER(sys/soundcard.h,
				CFLAGS="$CFLAGS -DSYS_SOUNDCARD_H")
			AC_CHECK_LIB(ossaudio, _oss_ioctl, 
				LIBS="$LIBS -lossaudio")
			;;
                *-*-osf*)
                        ad_files="ad_osf.lo mulaw_base.lo"
                        ad_backend="AD_BACKEND_OSF"
                        CFLAGS="$CFLAGS -DAD_BACKEND_OSF"
                        ;;
                *-*-irix*)
                        ad_files="ad_irix.lo mulaw_base.lo"
                        ad_libs="-laudio"
                        ad_backend="AD_BACKEND_IRIX"
                        CFLAGS="$CFLAGS -DAD_BACKEND_IRIX"
                        ;;
                *-*-sunos4*)
                        ad_files="ad_sunos.lo audio_utils_sunos.lo"
                        ad_libs="-lm -lnsl -ldl"
                        ad_backend="AD_BACKEND_SUNOS"
                        CFLAGS="$CFLAGS -DSUN4 -DAD_BACKEND_SUNOS"
                        ;;
                *86*-*-solaris*)
                        ad_files="ad_sunos.lo audio_utils_sunos.lo"
                        ad_libs="-lm -lnsl -lsocket -ldl"
                        ad_backend="AD_BACKEND_SUNOS"
                        CFLAGS="$CFLAGS -DAD_BACKEND_SUNOS"
                        ;;
                *-*-solaris*)
                        ad_files="ad_sunos.lo audio_utils_sunos.lo"
                        ad_libs="-lm -lnsl -lsocket -ldl"
                        ad_backend="AD_BACKEND_SUNOS"
                        CFLAGS="$CFLAGS -DAD_BACKEND_SUNOS"
                        ;;
                *-*-*cygwin*)
                        ad_files="play_win32.lo rec_win32.lo"
                        ad_libs="-lwinmm"
                        ad_backend="AD_BACKEND_WIN32"
                        CFLAGS="$CFLAGS -DAD_BACKEND_WIN32"
                        ;;
                *-*apple*-*)
                        ad_files="ad_portaudio.lo pa_convert.lo pa_lib.lo \
				  pa_mac_core.lo pa_trace.lo pablio.lo ringbuffer.lo"
                        ad_backend="AD_BACKEND_PORTAUDIO"
			ad_libs="-framework AudioToolbox -framework CoreAudio"
			# We must remove -ansi because OS X headers don't like it
                        CFLAGS="`echo $CFLAGS -DAD_BACKEND_PORTAUDIO | sed -e s,-ansi,,`"
                        ;;
                *)
                        ad_files="ad_base.lo"
                        ad_backend="AD_BACKEND_NONE"
                        AC_MSG_RESULT([No audio interface for host type $host.])
                        CFLAGS="$CFLAGS -DAD_BACKEND_NONE"
                        ;;
        esac
fi

AC_PATH_PROG(PERL,perl)
AC_PATH_PROG(CSH,csh)
AC_SUBST(docdata)
AC_SUBST(hmmdata)
AC_SUBST(lmdata)
AC_SUBST(tidigitsdata)
AC_SUBST(ad_files)
AC_SUBST(ad_libs)
AC_SUBST(ad_backend)
AC_SUBST(PERL)
AC_SUBST(CSH)

AM_PROG_LIBTOOL

AC_OUTPUT([
Makefile
src/Makefile
src/libs3audio/Makefile
src/libs3audio/ad_conf.h
src/libs3decoder/Makefile
src/libs3decoder/libAPI/Makefile
src/libs3decoder/libam/Makefile
src/libs3decoder/libcep_feat/Makefile
src/libs3decoder/libconfidence/Makefile
src/libs3decoder/libcommon/Makefile
src/libs3decoder/libdict/Makefile
src/libs3decoder/libep/Makefile
src/libs3decoder/liblm/Makefile
src/libs3decoder/libsearch/Makefile
src/libs3decoder/libcfg/Makefile
src/libutil/Makefile
src/programs/Makefile
src/tests/Makefile
src/tests/performance/Makefile
src/tests/performance/an4/Makefile
src/tests/performance/hub4/Makefile
src/tests/performance/rm1/Makefile
src/tests/performance/wsj20k/Makefile
src/tests/performance/wsj5k/Makefile
src/tests/performance/tidigits/Makefile
src/tests/performance/ti46/Makefile
src/tests/performance/Communicator/Makefile
src/tests/performance/ICSI/Makefile
src/tests/performance/spoke3/Makefile
src/tests/programs/Makefile
src/tests/regression/Makefile
src/tests/regression/checkStyle.sh
src/tests/regression/test-livepretend.sh
src/tests/regression/test-livepretend-clm.sh
src/tests/regression/test-livepretend-mllr.sh
src/tests/regression/test-lm_convert.sh
src/tests/regression/test-align.sh
src/tests/regression/test-align-mllr.sh
src/tests/regression/test-allphone.sh
src/tests/regression/test-allphone-mllr.sh
src/tests/regression/test-astar.sh
src/tests/regression/test-astar-clm.sh
src/tests/regression/test-cepview.sh
src/tests/regression/test-conf.sh
src/tests/regression/test-dag.sh
src/tests/regression/test-dag-clm.sh
src/tests/regression/test-decode_anytopo.sh
src/tests/regression/test-decode_anytopo-mllr.sh
src/tests/regression/test-decode_anytopo-2ndstage.sh
src/tests/regression/test-decode.sh
src/tests/regression/test-decode-simple.sh
src/tests/regression/test-decode-noncomp.sh
src/tests/regression/test-decode-lts.sh
src/tests/regression/test-decode-mode2.sh
src/tests/regression/test-decode-mode3.sh
src/tests/regression/test-decode-mode3-mulmpx.sh
src/tests/regression/test-decode-mode3-mpx.sh
src/tests/regression/test-decode-mode5.sh
src/tests/regression/test-decode-mode5-noncomp.sh
src/tests/regression/test-decode-mode1368.sh
src/tests/regression/test-decode-mode1369.sh
src/tests/regression/test-decode-s3cont.sh
src/tests/regression/test-decode-s2semi.sh
src/tests/regression/test-decode-clm.sh
src/tests/regression/test-decode-mllr.sh
src/tests/regression/test-decode-2ndstage.sh
src/tests/regression/test-lm_convert-and-decode.sh
src/tests/regression/test-dp.sh
src/tests/regression/test-ep.sh
src/tests/regression/test-gausubvq.sh
src/tests/regression/test-wave2feat.sh
src/tests/regression/test-wave2feat-dither-seed.sh
src/tests/regression/test-wave2feat-dither-rand.sh
src/tests/regression/test-wave2feat-logspec.sh
src/tests/unit_tests/Makefile
src/tests/unit_tests/test_case/Makefile
src/tests/unit_tests/test_hash/Makefile
src/tests/unit_tests/test_logs3/Makefile
scripts/Makefile
scripts/sphinx3-simple
model/Makefile
model/ep/Makefile
model/hmm/Makefile
model/hmm/hub4_cd_continuous_8gau_1s_c_d_dd/Makefile
model/hmm/tidigits/Makefile
model/hmm/RM1_cd_semi/Makefile
model/lm/Makefile
model/lm/an4/Makefile
model/lm/an4/args.an4
model/lm/an4/args.an4.test
model/lm/an4/args.an4.test.cls
model/lm/an4/args.an4.test.mllr
model/lm/an4/an4.ug.cls.lmctl
include/Makefile
doc/Makefile])

chmod +x scripts/sphinx3-simple
chmod +x src/tests/regression/*.sh
