dnl Welcome to the Sphinx-3 automated build system.
dnl try not to hurt yourself ;)

AC_INIT(configure.in)
AM_INIT_AUTOMAKE(sphinx3,0.5)
AM_CONFIG_HEADER(config.h)

CFLAGS=${CFLAGS:--g -O2 -Wall}

AC_ISC_POSIX
AC_PROG_CC
AM_PROG_CC_STDC
AC_HEADER_STDC

AC_TYPE_SIGNAL

hmmdata=`ls -1 $srcdir/model/hmm/hub4_cd_continuous_8gau_1s_c_d_dd | egrep -v '(Makefile|CVS)'`
hmmdata=`echo $hmmdata`

lmdata=`ls -1 $srcdir/model/lm/an4 | egrep -v '(Makefile|CVS)' | sed 's/\.in//g'`
lmdata=`echo $lmdata`

docdata=`ls -1 $srcdir/doc | egrep -v '(Makefile|CVS)'`
docdata=`echo $docdata`

AC_CANONICAL_HOST


dnl
dnl determine audio type or use none if none supported on this platform
dnl

ad_files=""
ad_libs=""

AC_C_BIGENDIAN([audio_test_file="pittsburgh.bigendian"
		AC_DEFINE(__BIG_ENDIAN__,[],[Host is big-endian])],
	       [audio_test_file="pittsburgh.littleendian"
		AC_DEFINE(__LITTLE_ENDIAN__,[],[Host is little-endian])])

dnl Added from suggestion by 
dnl Jasper van Veghel <jvveghel@vanboxtelsoftware.nl>, 02/03/2003:
AC_ARG_WITH(alsa,
           AC_HELP_STRING([--with-alsa], [Use ALSA library for sound I/O]),
           [ad_files="ad_alsa.lo",
            ad_backend="AD_BACKEND_ALSA",
            ad_libs="-lasound",
                   CFLAGS="$CFLAGS -DAD_BACKEND_ALSA",
            AC_CHECK_HEADER(sys/asoundlib.h, 
                    AC_ERROR(ALSA header file <sys/asoundlib.h> not found!))]
)

if test "x$ad_files" = "x"; then
        case $host in
                *-*-linux*)
                        ad_files="ad_oss.lo"
                        ad_backend="AD_BACKEND_OSS"
			AC_DEFINE(AD_BACKEND_OSS,[],[Use OSS audio routines])
                        ;;
                # FIXME: isn't this the same OSS as on Linux?
                *-*-freebsd*|*-*-netbsd*|*-*-openbsd*)
                        ad_files="ad_oss_bsd.lo mulaw_base.lo"
                        ad_backend="AD_BACKEND_OSS_BSD"
			AC_DEFINE(AD_BACKEND_OSS_BSD,[],[Use OSS audio routines (BSD version)])
                        ;;
                *-*-osf*)
                        ad_files="ad_osf.lo mulaw_base.lo"
                        ad_backend="AD_BACKEND_OSF"
			AC_DEFINE(AD_BACKEND_OSF,[],[Use Tru64 audio routines])
                        ;;
                *-*-irix*)
                        ad_files="ad_irix.lo mulaw_base.lo"
                        ad_libs="-laudio"
                        ad_backend="AD_BACKEND_IRIX"
			AC_DEFINE(AD_BACKEND_IRIX,[],[Use IRIX audio routines])
                        ;;
                *-*-sunos4*)
                        ad_files="ad_sunos.lo audio_utils_sunos.lo"
                        ad_libs="-lm -lnsl -ldl"
                        ad_backend="AD_BACKEND_SUNOS"
			AC_DEFINE(SUN4,[],[Use SunOS 4.x audio])
			AC_DEFINE(AD_BACKEND_SUNOS,[],[Use SunOS/Solaris audio routines])
                        ;;
                *86*-*-solaris*)
                        ad_files="ad_sunos.lo audio_utils_sunos.lo"
                        ad_libs="-lm -lnsl -lsocket -ldl"
                        ad_backend="AD_BACKEND_SUNOS"
			AC_DEFINE(AD_BACKEND_SUNOS,[],[Use SunOS/Solaris audio routines])
                        ;;
                *-*-solaris*)
                        ad_files="ad_sunos.lo audio_utils_sunos.lo"
                        ad_libs="-lm -lnsl -lsocket -ldl"
                        ad_backend="AD_BACKEND_SUNOS"
			AC_DEFINE(AD_BACKEND_SUNOS,[],[Use SunOS/Solaris audio routines])
                        ;;
                *-*-*cygwin*)
                        ad_files="play_win32.lo rec_win32.lo"
                        ad_libs="-lwinmm"
                        ad_backend="AD_BACKEND_WIN32"
			AC_DEFINE(AD_BACKEND_WIN32,[],[Use Win32 audio routines])
                        ;;
                *-*apple*-*)
                        ad_files="ad_base.lo"
                        ad_backend="AD_BACKEND_NONE"
			AC_DEFINE(AD_BACKEND_NONE,[],[No audio routines for this platform])
                        ;;
                *)
                        ad_files="ad_base.lo"
                        ad_backend="AD_BACKEND_NONE"
                        AC_MSG_RESULT([No audio interface for host type $host.])
			AC_DEFINE(AD_BACKEND_NONE,[],[No audio routines for this platform])
                        ;;
        esac
fi

AC_PATH_PROG(PERL,perl)
AC_PATH_PROG(CSH,csh)
AC_SUBST(hmmdata)
AC_SUBST(lmdata)
AC_SUBST(docdata)
AC_SUBST(ad_files)
AC_SUBST(ad_libs)
AC_SUBST(ad_backend)
AC_SUBST(audio_test_file)
AC_SUBST(PERL)
AC_SUBST(CSH)

AM_PROG_LIBTOOL

AC_OUTPUT([
Makefile
src/Makefile
src/libs3audio/Makefile
src/libs3audio/ad_conf.h
src/libs3decoder/Makefile
src/libutil/Makefile
src/programs/Makefile
scripts/Makefile
scripts/sphinx3-test
scripts/sphinx3-simple
model/Makefile
model/hmm/Makefile
model/hmm/hub4_cd_continuous_8gau_1s_c_d_dd/Makefile
model/lm/Makefile
model/lm/an4/Makefile
model/lm/an4/an4.ctl
model/lm/an4/args.an4
model/lm/an4/args.an4.test
doc/Makefile])

chmod +x scripts/sphinx3-simple
chmod +x scripts/sphinx3-test
